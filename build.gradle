buildscript {
    repositories {
        jcenter()
        maven { url "https://artifactory.smartling.net/artifactory/repo" }
    }
    dependencies {
        classpath 'com.smartling.gradle.plugins:sl-maven-upload:20160602.10.g5800337'
        classpath 'com.smartling.gradle.plugins:sl-encrypted-credentials:20160602.10.g5800337'
    }
}

plugins {
    id 'java'
    id "org.unbroken-dome.test-sets" version "2.1.1"
    id 'com.palantir.git-version' version '0.11.0'
}

apply plugin: 'com.smartling.sl-encrypted-credentials'
apply plugin: 'com.smartling.sl-maven-upload'
apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

description = 'Hubspot SDK'
group = 'com.smartling.connector'
version = gitVersion()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'io.github.openfeign:feign-core:10.2.3'
    compile 'io.github.openfeign:feign-gson:10.2.3'
    compile 'io.github.openfeign:feign-httpclient:10.2.3'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.0'

    compileOnly "org.projectlombok:lombok:1.18.8"
    annotationProcessor "org.projectlombok:lombok:1.18.8"

    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'junit:junit:4.12'
    testCompile 'com.jayway.jsonpath:json-path:2.0.0'
    testCompile 'com.jayway.jsonpath:json-path-assert:2.0.0'
    testCompile 'com.github.tomakehurst:wiremock:1.56'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'

    testCompileOnly "org.projectlombok:lombok:1.18.8"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.8"
}

test {
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    minHeapSize = '512m'
    maxHeapSize = '1024m'

    // Uncomment the following jvmArgs line if you want to use a debugger during tests. It will stop execution on failure, and let you connect.
    // NOTE: Make sure to put the comment back before you commit!!!
    //jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005'

    outputs.upToDateWhen { false }
}

// workaround to start tests job with passed system properties
tasks.withType(Test) {
    systemProperties = System.properties
}

testSets {
    integrationTest {}
}

// TODO enable int tests when we have long-term working sandbox env
//check.dependsOn integrationTest

task setCredentials {
    doLast {
        project.ext.set('mavenCredentials', [
            'smartling.repository.source.local-overwritable-releases':
                    [
                            'username': 'connectors-ci',
                            'password': "$System.env.ci_pass_releases" //uses jenkins keystore, passed through pipeline using 'withCredentials'
                    ],

            'smartling.repository.target.snapshots':
                    [
                            'username': 'continuous_integration',
                            'password': "$System.env.ci_pass" //uses jenkins keystore, passed through pipeline using 'withCredentials'
                    ]
        ])
    }
}

uploadArchives.dependsOn setCredentials
